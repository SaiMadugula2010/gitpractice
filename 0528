proc datasets lib= work kill nolist ; run; quit; 
%macro adddata(newdata);
%if &newdata>0 %then %do; 
/*data days; */
/*dc="'"||put('30jun2022'd,yymmdd10.)||"'";output;*/
/*run;*/
data days; set days end= eof; 
 call execute ('%databyday('||dc||','||left(_n_)||');');
 if eof then do; 
   call execute('data graphall; set final1-final'||left(_n_)||'; run;');
  call execute('proc datasets lib=work; delete top5final1-top5final'||left(_n_)||' graphfinal1-graphfinal'||left(_n_)||'final1-final'||left(_n_)||';run;quit; ');
  call symput ('monthend',put(d,monyy7.));
 end; 
run;
 

 data graph; set org graphall; format Total Futures CDS IRS fo_c fo_h cds_c cds_h irs_c irs_h comma20.;
 format FO_top5 Swaps_top5 Total_Top5 percent8.1;
run;

ods excel file="\\cftc.gov\dfssas\PRD\CFTC\Data\GROUPS\DCR\MonthlyClearedMargin\CFTC ClearedMarginReport Data_&monthend..xlsx"
 options (sheet_name='CFTC ClearedMargin Data' embedded_titles='no' )style=Dove;
proc report  data=graph nowindows headline split='_';
 columns date Total Futures CDS IRS fo_c fo_h cds_c cds_h irs_c irs_h FO_top5 Swaps_top5 Total_Top5;
 define date/'Business_Date' format=mmddyy10. order order=data;
 define fo_c/'Customer_Futures';
 define fo_h/'House_Futures';
 define cds_c/'Customer_CDS';
 define cds_h/'House_CDS';
 define irs_c/'Customer_IRS';
 define irs_H/'House_IRS';
 define fo_top5/'Top5 %_Futures';
 define swaps_top5 /'Top5 %_Swaps';
 define total_top5/'Top5 %_Total';
run;
ods excel close; 


ods excel file="\\cftc.gov\dfssas\PRD\CFTC\Data\GROUPS\DCR\MonthlyClearedMargin\CFTC ClearedMarginReport Data_Current.xlsx"
 options (sheet_name='CFTC ClearedMargin Data' embedded_titles='no' )style=Dove;
proc report  data=graph nowindows headline split='_';
 columns date Total Futures CDS IRS fo_c fo_h cds_c cds_h irs_c irs_h FO_top5 Swaps_top5 Total_Top5;
 define date/'Business_Date' format=mmddyy10. order order=data;
 define fo_c/'Customer_Futures';
 define fo_h/'House_Futures';
 define cds_c/'Customer_CDS';
 define cds_h/'House_CDS';
 define irs_c/'Customer_IRS';
 define irs_H/'House_IRS';
 define fo_top5/'Top5 %_Futures';
 define swaps_top5 /'Top5 %_Swaps';
 define total_top5/'Top5 %_Total';
run;
ods excel close; 
data dco.monthlyclearedmargin; set graph; run;


proc template;
define style styles.nobreak;
	parent=Styles.Dove;
	style body from body /
		pagebreakhtml=_undef_;
end;
run;
ods listing close; 
 options nocenter ps=max  ;
		FILENAME outbox EMAIL
		SUBJECT='***Monthly Cleared Margin Data Updated***'
		to=('cmarquardt@cftc.gov') type='text/html'   replyto='cmarquardt@cftc.gov'
	 
;
ods html  body=outbox rs=none style=nobreak nogtitle nogfootnote; 

proc odstext;
 p "Cleared Margin Data updated for &monthend.";
 p "<a href='\\dcprdapsasm02\CODE_DEV\CFTC\DCR\MonthlyClearedMarginReport'>Use plotPDF and text to create report</a>";
run;
ods html close; 
ods listing; 

%end;
%mend; 

 %macro cm(seg);
 proc sort data=top5; by reportingdate descending c&seg; run;
 data top5; set top5; by reportingdate;retain &seg.top5;
 if first.reportingdate then &seg.top5=1;
 if first.reportingdate=0 then &seg.top5+1; 
 if &seg.top5 <=5 then &seg.sel=c&seg; 
 run;
 %mend; 
 %macro databyday(rpdate,tb); 
Proc sql;
	connect to odbc (NOPROMPT='Driver={SQL Server Native Client 11.0}; read_lock_type=nolock; SERVER=LPRDDCODM; ApplicationIntent=ReadOnly;
		Trusted_Connection=yes;DATABASE=DCODM01DB ;schema=dbo');
	create table  rawdata as
		select reportingdate, clearingorganizationid as dco,marketsegment ,clearingfirmid,firmname,
		case when substr(clearingfundsegregationtype,1,1)='C' then 'C' else 'H' end as origin,
		-totalmarginamount as tm, 
		case marketsegment 
			when 'IRS' then 'Swaps'
			when 'CDS' then 'Swaps'
			when 'FuturesOptions' then 'FO'
	end as segment
from connection to odbc(
  DECLARE	@return_value int,
		@iErrorCode int

EXEC	@return_value = [dbo].[GetPart39AccountSummaryDataByDate]
		@fromDate = &rpdate,
		@toDate = &rpdate,
		@iErrorCode = @iErrorCode OUTPUT
 )	
where clearingorganizationid in ('CME', 'ICC', 'ICEU', 'ICUS',  'LCHSA','EUX') or (
 clearingorganizationid='LCHLTD' and marketsegment notin ('FuturesOptions','FX'))
 ;
quit; 
 


data rawdata; set rawdata; 
 if marketsegment='FuturesOptions' and dco not in ('CME','ICEU','ICUS') then delete; 
 if marketsegment='IRS' and dco not in ('CME','LCHLTD','EUX') then delete; 
 if marketsegment='CDS' and dco not in ('CME','ICC','ICEU','LCHSA') then delete; 
 if marketsegment in ('FuturesOptions','IRS','CDS') and tm >0;
run;
proc freq;tables marketsegment*dco/list;run;

data _null_; call symput ('ref',&rpdate);run;
libname x "\\cftc.gov\dfssas\PRD\CFTC\Data\GROUPS\DCR\ReferenceFile\&ref";
 
proc sql; 
 create table firm as 
 select distinct clearingorganizationid,clearingfirmid,case when firmparentname ='' then firmleiname else firmparentname end as name
 from x.leireferencemaster
 ;
 create table step1 as 
 select rawdata.*,name
 from rawdata left join firm
 on rawdata.dco=firm.clearingorganizationid and rawdata.clearingfirmid=firm.clearingfirmid;
quit; 
 
proc sql; 
 create table top5 as 
select distinct reportingdate,name,max(fo) as cfo,max(swaps) as cswaps,sum(calculated cfo,calculated cswaps) as ctot
from (
 select reportingdate, name,
	case when segment='FO' then sum(tm) end as fo,
	case when segment='Swaps' then sum(tm) end as swaps
 from step1
 where origin='C' 
 group by reportingdate,name, segment
 )
 group by reportingdate,name
;
 quit; 

 %cm(fo);%cm(swaps); %cm(tot); 

proc sql; 
 create table top5final&tb as 
 select reportingdate as date
	,sum(fosel)/sum(cfo) as Fo_top5 format=percent8.1
	,sum(swapssel)/sum(cswaps) as Swaps_top5 format=percent8.1
	,sum(totsel)/sum(ctot) as Total_top5 format=percent8.1
 from top5
group by reportingdate; 
quit; 

proc sql; 
create table graphfinal&tb as 
select distinct   a.reportingdate as date
	,max(a.cdstotalm) as cds format=comma20.
	,max(fototalm) as futures format=comma20.
	,max(a.irstotalm) as irs format=comma20.
	,sum(calculated cds,calculated irs+calculated futures) as total format=comma20.,
	customer_cds as cds_c format=comma20.,house_cds as cds_h  format=comma20.,
	customer_irs as irs_c format=comma20.,house_irs as irs_h format=comma20.,
	customer_fo as fo_c format=comma20.,house_fo as fo_h format=comma20.
from (
select t1.reportingdate, sum(t1.cds,t2.cds) as cdstotalm,sum(t1.irs,t2.irs) as irstotalm,sum(t1.fo,t2.fo) as fototalm,
	max(t1.cds) as customer_cds,max(t1.irs) as customer_irs,max(t1.fo) as customer_fo,
	max(t2.cds) as house_cds,max(t2.irs) as house_irs,max(t2.fo) as house_fo

from
(
 select distinct reportingdate,
 case  when marketsegment='CDS' then sum(tm) end as cds  ,
 case  when marketsegment='IRS' then sum(tm) end as irs ,
 case  when marketsegment='FuturesOptions' then sum(tm) end as fo  
 from step1
 where origin='C'
 group by reportingdate,marketsegment
 ) t1 left join 
 (
 select distinct reportingdate,
 case  when marketsegment='CDS' then sum(tm) end as cds  ,
 case  when marketsegment='IRS' then sum(tm) end as irs ,
 case  when marketsegment='FuturesOptions' then sum(tm) end as fo  
 from step1
 where origin='H'
 group by reportingdate,marketsegment
) t2
 on t1.reportingdate=t2.reportingdate
 group by t1.reportingdate
) as a

; quit; 

data final&tb; merge graphfinal&tb top5final&tb;run;

%mend; 


libname dco meta library="SAS - DCR DCO Report" metaout=data;
 data org; set dco.monthlyclearedmargin; where date ne .;  run;
/*Add EUX historical data*/
/*proc import datafile='\\cftc.gov\dfssas\PRD\CFTC\Data\GROUPS\DCR\MonthlyClearedMargin\tmp\CFTC ClearedMarginReport Data3EUXFrom2020_Aug2022.xlsx'*/
/* dbms=xlsx out=tt replace ; run;*/
/*proc sql; */
/* select "'"||trim(name)||"'n" into :aa separatd by ' ' from sashelp.vcolumn*/
/* where libname='WORK' and memname='TT';*/
/*quit; */
/*data tta; set tt; */
/*format date date.;*/
/* array a date Total Futures CDS IRS fo_c fo_h cds_c cds_h irs_c irs_h FO_top5 Swaps_top5 Total_Top5; */
/* array b &aa; */
/* do over a; a=b;end; */
/* keep date Total Futures CDS IRS fo_c fo_h cds_c cds_h irs_c irs_h FO_top5 Swaps_top5 Total_Top5;*/
/*run;*/
/* Data prior to adding EUX data dco.monthlyclearedmargin_NoEUX; set org; run;*/
/*data dco.monthlyclearedmargin; set tta; run;*/

data t;set org end=eof; 
 if eof; 
 call symput ('lastmonth',intnx('MONTH', date, 1, 'Begin'));
 run;
 %put &=lastmonth;run;

 data days; 
  a=intnx('MONTH', today(), 0, 'Begin');
	do d=&lastmonth to a-1; 
		y=year(d);m=month(d); dc="'"||put(d,yymmdd10.)||"'";
 		if weekday(d) in (2:6) then output;	 
	end; 
run;
proc sort; by y m descending d; 
data days; set days; by y m; if first.m; run;
%let runit=0;
proc sql; 
 select count(*) into :runit
 from days; 
quit; 
%adddata(&runit);
































import pyodbc
import pandas as pd
import datetime as dt
from pathlib import Path
import smtplib
from email.mime.text import MIMEText
from email.mime.multipart import MIMEMultipart

# ---------------------------------------------------
# 1. SQL Connection
# ---------------------------------------------------
def get_sql_connection():
    conn = pyodbc.connect(
        "Driver={SQL Server Native Client 11.0};"
        "Server=LPRDDCODM;"
        "Database=DCODM01DB;"
        "Trusted_Connection=yes;"
    )
    return conn

# ---------------------------------------------------
# 2. Extract Daily Data (replicates %databyday)
# ---------------------------------------------------
def get_daily_data(report_date: str):
    query = f"""
    DECLARE @return_value int, @iErrorCode int;
    EXEC @return_value = dbo.GetPart39AccountSummaryDataByDate
        @fromDate = '{report_date}',
        @toDate   = '{report_date}',
        @iErrorCode = @iErrorCode OUTPUT;
    """
    conn = get_sql_connection()
    df = pd.read_sql(query, conn)
    conn.close()

    # Business rules filtering
    allowed_dcos = ['CME', 'ICC', 'ICEU', 'ICUS', 'LCHSA', 'EUX']
    df = df[df['clearingorganizationid'].isin(allowed_dcos)]

    # Example segment mapping
    df['segment'] = df['marketsegment'].map({
        'IRS': 'Swaps',
        'CDS': 'Swaps',
        'FuturesOptions': 'FO'
    })

    return df

# ---------------------------------------------------
# 3. Transformation (top5 logic)
# ---------------------------------------------------
def get_top5(df: pd.DataFrame, segment: str):
    """Compute top 5 firms for FO, Swaps, Totals"""
    sub = df[df['segment'] == segment]
    grouped = sub.groupby(['reportingdate', 'firmname']).agg({'tm':'sum'}).reset_index()
    grouped['rank'] = grouped.groupby('reportingdate')['tm'].rank(ascending=False, method='first')
    top5 = grouped[grouped['rank'] <= 5]
    return top5

# ---------------------------------------------------
# 4. Monthly aggregation
# ---------------------------------------------------
def process_month(dates: list):
    all_graphs = []
    for i, d in enumerate(dates, start=1):
        df = get_daily_data(d.strftime("%Y-%m-%d"))

        # Compute top5s
        top5_fo = get_top5(df, 'FO')
        top5_swaps = get_top5(df, 'Swaps')

        # Example aggregation
        totals = df.groupby(['reportingdate','segment','origin'])['tm'].sum().unstack(fill_value=0)
        totals.reset_index(inplace=True)

        all_graphs.append(totals)

    final_df = pd.concat(all_graphs, ignore_index=True)
    return final_df

# ---------------------------------------------------
# 5. Write Excel (replicates ODS EXCEL)
# ---------------------------------------------------
def write_excel(df: pd.DataFrame, outpath: Path, sheet_name="CFTC ClearedMargin Data"):
    with pd.ExcelWriter(outpath, engine="xlsxwriter") as writer:
        df.to_excel(writer, sheet_name=sheet_name, index=False)

# ---------------------------------------------------
# 6. Send Email
# ---------------------------------------------------
def send_email(subject, body, to_emails):
    sender = "cmarquardt@cftc.gov"
    msg = MIMEMultipart()
    msg["From"] = sender
    msg["To"] = ", ".join(to_emails)
    msg["Subject"] = subject

    msg.attach(MIMEText(body, "html"))

    with smtplib.SMTP("your_smtp_server", 25) as server:
        server.sendmail(sender, to_emails, msg.as_string())

# ---------------------------------------------------
# 7. Driver
# ---------------------------------------------------
if __name__ == "__main__":
    today = dt.date.today()
    first_of_month = today.replace(day=1)

    # Example: process previous monthâ€™s weekdays
    start = first_of_month - dt.timedelta(days=1)
    start = start.replace(day=1)
    dates = pd.date_range(start=start, end=today, freq="B")  # business days

    df_final = process_month(dates)

    monthend = today.strftime("%b%Y")
    out1 = Path(f"CFTC ClearedMarginReport Data_{monthend}.xlsx")
    out2 = Path("CFTC ClearedMarginReport Data_Current.xlsx")

    write_excel(df_final, out1)
    write_excel(df_final, out2)

    send_email(
        subject="***Monthly Cleared Margin Data Updated***",
        body=f"<p>Cleared Margin Data updated for {monthend}.</p>"
             f"<p><a href='\\\\dcprdapsasm02\\CODE_DEV\\CFTC\\DCR\\MonthlyClearedMarginReport'>Use plotPDF and text to create report</a></p>",
        to_emails=["cmarquardt@cftc.gov"]
    )
